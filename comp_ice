#! /bin/csh -f

#Change these to your own site and user directory! 
setenv SITE LANL
setenv SYSTEM_USERDIR /scratch/lipscomb

# MPI runs
setenv COMMDIR mpi 

# Single processor runs
#setenv COMMDIR serial

#ESMF runs
#setenv USE_ESMF yes
setenv USE_ESMF no

#CAM runs
#setenv CAM_ICE yes
setenv CAM_ICE no

#CSM shared code
setenv SHRDIR csm_share

# Set SRCDIR and EXEDIR to your own paths!
#setenv SRCDIR $HOME/cice.v4.0/cice
setenv SRCDIR /net/bob4/lipscomb/cice_svn/cice
setenv EXEDIR $SYSTEM_USERDIR/rundir.v4.0 ; if !(-d $EXEDIR) mkdir -p $EXEDIR

setenv CBLD   $SRCDIR/bld
setenv OBJDIR $EXEDIR/compile           ; if !(-d $OBJDIR) mkdir -p $OBJDIR
setenv RSTDIR $EXEDIR/restart           ; if !(-d $RSTDIR) mkdir -p $RSTDIR

setenv ARCH `uname -s`
#if ( $SITE == 'LANL') setenv ARCH $ARCH.LANL
if ( $ARCH == 'UNICOS/mp') setenv ARCH UNICOS
if ( $ARCH == 'UNICOS') then
   cp -f $CBLD/Makefile.$ARCH $CBLD/Makefile
else
   cp -f $CBLD/Makefile.std $CBLD/Makefile
endif

cd $EXEDIR

if !(-e grid)             cp $SRCDIR/input_templates/global_100x116.grid grid
if !(-e kmt)              cp $SRCDIR/input_templates/global_100x116.kmt kmt
if !(-e ice_in)           cp $SRCDIR/input_templates/ice_in.$ARCH ice_in
if !(-e run_ice)          cp $SRCDIR/input_templates/run_ice.$ARCH run_ice

cd $RSTDIR

if !(-e iced_gx3_v3.1)    cp $SRCDIR/input_templates/iced_gx3_v3.1 .
if !(-e ice.restart_file) cp $SRCDIR/input_templates/ice.restart_file .

cd $OBJDIR

# Filepath: List of source code directories (in order of importance).
cat >! Filepath << EOF
$SRCDIR/source
$SRCDIR/$COMMDIR
$SRCDIR/$SHRDIR
EOF

cc -o makdep $CBLD/makdep.c                              || exit 2
gmake VPFILE=Filepath EXEC=$EXEDIR/cice  \
      -f  $CBLD/Makefile MACFILE=$CBLD/Macros.$ARCH || exit 2

cd $SRCDIR

#! /bin/csh -f

### Change these to your own site and user directory! 
### You will need to create a Makefile Macro in bld/ and a run_ice script 
### in input_templates/.
setenv SITE LANL.conejo
#setenv SITE LANL.mustang

### SYSTEM_USERDIR is predefined on ORNL machines
setenv SYSTEM_USERDIR /scratch/eclare/CICE.v5.0  # conejo

### Grid resolution
#setenv RES col ; setenv GRID 5x5
setenv RES gx3 ; setenv GRID 100x116
#setenv RES gx1 ; setenv GRID 320x384
#setenv RES tx1 ; setenv GRID 360x240

set NXGLOB = `echo $GRID | sed s/x.\*//`
set NYGLOB = `echo $GRID | sed s/.\*x//`

# Recommendations:
#   NTASK equals nprocs in ice_in 
#   use processor_shape = slenderX1 or slenderX2 in ice_in
#   one per processor with distribution_type='cartesian' or
#   squarish blocks with distribution_type='rake'
# If BLCKX (BLCKY) does not divide NXGLOB (NYGLOB) evenly, padding 
# will be used on the right (top) of the grid.
if ($RES == 'col') then   # for column configuration:
   setenv NTASK      1       # total number of processors
   setenv BLCKX      5       # x-dimension of blocks ( not including )
   setenv BLCKY      5       # y-dimension of blocks (  ghost cells  )
   setenv NICELYR   100       # number of vertical layers in the ice
   setenv NSNWLYR    1       # number of vertical layers in the snow
   setenv NICECAT    1       # number of ice thickness categories
else                      # test problem: gx3 - serial
   setenv NTASK      4       # total number of processors
   setenv BLCKX     25       #20 x-dimension of blocks ( not including )
   setenv BLCKY     29       #25 y-dimension of blocks (  ghost cells  )
   setenv NICELYR    4      # number of vertical layers in the ice
   setenv NSNWLYR    1       # number of vertical layers in the snow
   setenv NICECAT    5       # number of ice thickness categories
endif

# may need to increase MXBLCKS with rake distribution or padding
@ a = $NXGLOB * $NYGLOB ; @ b = $BLCKX * $BLCKY * $NTASK  
@ m = $a / $b ; setenv MXBLCKS $m; if ($MXBLCKS == 0) setenv MXBLCKS 1

#setenv MXBLCKS 37 # if necessary (code will print proper value)

### Tracers               # match ice_in tracer_nml to conserve memory
setenv TRBRI   0          # set to 1 for brine height tracer
setenv TRAGE   0          # set to 1 for ice age tracer
setenv TRFY    0          # set to 1 for first-year ice area tracer
setenv TRLVL   0          # set to 1 for level and deformed ice tracers
setenv TRPND   0          # set to 1 for melt pond tracers
setenv NTRAERO 0          # number of aerosol tracers 
                          # (up to max_aero in ice_domain_size.F90) 
                          # CESM uses 3 aerosol tracers
setenv NBGCLYR 7          # number of biogeochemistry layers

### Specialty code
setenv CAM_ICE  no        # set to yes for CAM runs (single column) 
setenv SHRDIR   csm_share # location of CCSM shared code
setenv NETCDF   yes       # set to no if netcdf library is unavailable
setenv DITTO    no        # reproducible diagnostics
setenv ONED     no        # true 1D thermodynamic calculation

### Set SRCDIR and EXEDIR to your own paths!
setenv SRCDIR /usr/projects/climate/eclare/CICE.v5.0/cice  # conejo

setenv EXEDIR $SYSTEM_USERDIR/rundir
#setenv EXEDIR $SYSTEM_USERDIR/$RES
                                          if !(-d $EXEDIR) mkdir -p $EXEDIR
setenv CBLD   $SRCDIR/bld
setenv OBJDIR $EXEDIR/compile           ; if !(-d $OBJDIR) mkdir -p $OBJDIR
setenv RSTDIR $EXEDIR/restart           ; if !(-d $RSTDIR) mkdir -p $RSTDIR
setenv HSTDIR $EXEDIR/history           ; if !(-d $HSTDIR) mkdir -p $HSTDIR

setenv ARCH `uname -s`
if ( $ARCH == 'UNICOS/mp') setenv ARCH UNICOS
if ( $ARCH == 'UNICOS') then
   cp -f $CBLD/Makefile.$ARCH $CBLD/Makefile
else if ( $ARCH == 'Darwin' ) then
   cp -f $CBLD/Makefile.$ARCH $CBLD/Makefile
else
   cp -f $CBLD/Makefile.std $CBLD/Makefile
endif
setenv ARCH $ARCH.$SITE

cd $SRCDIR/source

cd $EXEDIR

if !($RES == 'col') then
if !(-e grid)    cp $SRCDIR/input_templates/$RES/global_$RES.grid grid
if !(-e kmt)     cp $SRCDIR/input_templates/$RES/global_$RES.kmt kmt
if !(-e eap_stresses) cp $SRCDIR/input_templates/eap_stresses eap_stresses
endif
if !(-e ice_in)  cp $SRCDIR/input_templates/$RES/ice_in .
if !(-e run_ice) cp $SRCDIR/input_templates/run_ice.$ARCH run_ice

cd $RSTDIR

cp $SRCDIR/input_templates/$RES/iced_$RES* .
if !(-e ice.restart_file) cp $SRCDIR/input_templates/$RES/ice.restart_file .

cd $OBJDIR

if ($NTASK == 1) then
   setenv COMMDIR serial
else
   setenv COMMDIR mpi 
endif

setenv DRVDIR cice
if ($NETCDF == 'yes') then
  setenv IODIR io_netcdf
else
  setenv IODIR io_binary
endif

### List of source code directories (in order of importance).
cat >! Filepath << EOF
$SRCDIR/drivers/$DRVDIR
$SRCDIR/source
$SRCDIR/$COMMDIR
$SRCDIR/$IODIR
$SRCDIR/$SHRDIR
EOF

cc -o makdep $CBLD/makdep.c                         || exit 2

gmake VPFILE=Filepath EXEC=$EXEDIR/cice \
           NXGLOB=$NXGLOB NYGLOB=$NYGLOB \
           BLCKX=$BLCKX BLCKY=$BLCKY MXBLCKS=$MXBLCKS \
      -f  $CBLD/Makefile MACFILE=$CBLD/Macros.$ARCH || exit 2

cd ..
pwd                                         
echo NTASK = $NTASK
echo "global N, block_size" 
echo "x    $NXGLOB,    $BLCKX"
echo "y    $NYGLOB,    $BLCKY"
echo max_blocks = $MXBLCKS

